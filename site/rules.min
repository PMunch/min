'hastysite import

;Routing
(
  =meta
  meta /id :id
  meta /ext :ext
  (
    ((id "home" ==) (
      meta "index" %id #meta
      meta ".html" %ext #meta
    ))
    ((ext ".md" ==) (
      meta ".html" %ext #meta
      meta "$1/index" (id) => % %id #meta
    ))
  ) case
  meta
) :set-destination

;Processing operators
(
  :tpl
  =meta
  meta /path :path
  " - Processing $1: $2" (tpl path) => % notice
  meta input-fread :contents
  meta settings /title %site #meta
  meta settings /version %version #meta
  contents meta markdown @contents
  meta contents %contents #meta
  tpl meta mustache :page
  meta page %contents #meta
  meta
) :process-md-with-template

(
  =meta
  meta /content-type :ct
  meta ct process-md-with-template
) :process-md-content

(
  =meta
  meta /path :path
  " - Processing stylesheet: $1" (path) => % notice
  meta input-fread :contents
  contents preprocess-css @contents
  meta contents %contents #meta
  meta
) :preprocess-css-content

(
  =meta
  meta /ext :ext
  meta
  (
    ((".md" ext ==) (process-md-content))
  ) case
) :process-content

;Main
assets (
  =meta
  meta /ext :ext
  meta
  (
    ((".css" ext ==) (
      preprocess-css-content
      output-fwrite
    ))
    ((true) (copy2output))
  ) case
) foreach

contents (
  dup
  (
    ((/id "^_includes" match) ()) ;Ignore files starting with underscore.
    ((true) (process-content set-destination output-fwrite)) 
  ) case
) foreach