'test load
'test import

newline 
symbols size :total-symbols
sigils size :total-sigils
"Total Symbols: $1" (total-symbols) % puts! 
"Total Sigils: $1" (total-sigils) % puts!
newline 

"lang" describe

  (2 'a define
    (3 a + (5 'a define a) -> +) -> a + 12 ==) assert

  (symbols "a" contains false ==) assert
  
  5 :five
  (symbols "five" contains) assert

  (
    ((1 2 3 4 5 6)) :test-data

    (
      :item
      "_$1" (item) => % :namesym
      (item dup *) namesym define
      namesym ROOT publish
    ) :def

    test-data (def) foreach

    _2 _5 _1 + +
    30 ==
  ) assert
  
  
  ~five
  (symbols "five" contains false  ==) assert

  (
    (+) :myplus
  ) +mymath

  (symbols "mymath" contains) assert
  

  (() inspect () ==) assert

  ('mymath import symbols "myplus" contains) assert

  ('mymath import 2 3 myplus 5 ==) assert

  (2 3 mymath ^myplus 5 ==) assert

  ; Extend an existing scope
  ;(
  ;  ('mymath import 
  ;   (-) :myminus
  ;  ) @mymath 5 2 mymath ^myminus 3 ==) assert
  ; 
  ;(mymath inspect ("myminus" "myplus") ==) assert

  ("3 4 +" eval 7 ==) assert

  ("2 2 +" "tests/testload.min" fwrite 'testload load 4 ==) assert
  "tests/testload.min" rm

  (2 2 mymath ^myplus 4 ==) assert
  
  (1 2 3 4 stack (1 2 3 4) ==) assert

  ((1 2 3) unstack stack (1 2 3) ==) assert

  ((1 2) (3 4) concat (1 2 3 4) ==) assert

  ((1 2 3) first 1 ==) assert
  
  ((1 2 3) rest (2 3) ==) assert

  (2 quote (2) ==) assert

  ((2 3) unquote stack (2 3) ==) assert

  (4 (1 2 3) append (1 2 3 4) ==) assert

  (0 (1 2 3) prepend (0 1 2 3) ==) assert

  ((1 2 3 4) 2 at 3 ==) assert
  ((1 2 3) size 3 ==) assert

  ((1 2 3 4) 5 contains false ==) assert
  ((1 2 3 4) 2 contains) assert

  ((1 2 3 4) (2 +) map (3 4 5 6) ==) assert

  (3 (succ) 3 times 6 ==) assert

  ((2 3 >) ("YES") ("NO") ifte "NO" ==) assert
  ((2 3 <) ("YES") ("NO") ifte "YES" ==) assert

  ("NO" (2 3 >) ("YES") ift "NO" ==) assert
  ((2 3 <) ("YES") ift "YES" ==) assert

  (0 :c
    (c 10 <) (c succ @c) while
    c 10 ==) assert

  ((1 2 3 4 5) (even?) filter (2 4) ==) assert

  (5 (dup 0 ==) (1 +) (dup 1 -) ( * ) linrec 120 ==) assert ;factorial of 5


  (
   (
    (pop) 
    ('error dget) 
    ("finally")
    ) try stack ("MinEmptyStackError" "finally") ==) assert 

  ("aaaa" :cd cd "aaaa" ==) assert ;It is possible to shadow sealed symbols in child scopes

  (((2 :a1 'a1 seal 3 :a1) ("failed")) try "failed" ==) assert

  (
    1 :a1
    'a1 seal
    'a1 unseal
    2 :a1
    2 a1 ==) assert

  (
   (
    (((error "TestError")(message "Test Message")) raise) 
    ('error dget)
    ) try "TestError" ==) assert

  (
   (("test" (1 2) :)) try stack ("test") ==) assert

  (
   (
    (() 1 at)
    (1)
    ) try 1 ==) assert

  ((a b +) (4 :a 5 :b) with 9 ==) assert

  ("{\"a\": 1, \"b\": 2.3}" from-json ((a 1) (b 2.3)) ==) assert

  ((1 2 3 "aaa" 'q q true) to-json "\n" "" replace " " "" replace "[1,2,3,\"aaa\",\";sym:'q\",\";sym:q\",true]"  ==) assert

  ((1 2 3 "aaa" 'q q true) to-json from-json (1 2 3 "aaa" 'q q true)  ==) assert

  (((a 1)(b 2)(c 3)) dictionary?) assert

  (((a 1)(b 2)(c 3)) 'b dget 2 ==) assert

  (((a 1)(b 2)(c 3)) ' :dict dict 5 'b dset 7 %d =newdict newdict ((a 1)(b 5)(c 3)(d 7)) ==  dict ((a 1)(b 2)(c 3)) == and) assert

  (((a 1)(b 2)(c 3)) ' :dict dict 'b ddel =newdict newdict ((a 1)(c 3)) == dict ((a 1)(b 2)(c 3)) == and) assert

  (((a 1)(b 2)(c 3)) keys (a b c) ==) assert

  (((a 1)(b 2)(c 3)) values (1 2 3) ==) assert

  (((1 2 3)) :sym1 >sym1 stored-symbols "sym1" contains) assert

  (<sym1 symbols "sym1" contains) assert

  ('sym1 remove-symbol stored-symbols "sym1" contains false ==) assert

  (0 :temp (1 2 3) (temp + @temp) foreach 6 temp ==) assert

  ((1 +) :mysucc 'mysucc source (1 +) ==) assert

  (6 
    (
      ((3 ==) (false))
      ((3 <) (false))
      ((3 >) (true))
    ) case
  ) assert

  ((3 4 7 2 4 6 5 6) '> sort  (2 3 4 4 5 6 6 7) ==) assert

  ((3 4 7 2 4 6 5 6) '< sort (7 6 6 5 4 4 3 2) ==) assert

  (time module-symbols ("datetime" "now" "tformat" "timeinfo" "timestamp") ==) assert

  (sys module-sigils ("!" "$" "&") ==) assert

  report
  ; Tidy up
  newstack
